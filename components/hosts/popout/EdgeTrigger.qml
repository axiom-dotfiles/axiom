pragma ComponentBehavior: Bound
import QtQuick
import Quickshell

import qs.config
import qs.services

// A thin hover strip on a screen edge that fires `triggered` once the
// pointer has rested on it for `hoverDelay`
PanelWindow {
  id: root

  signal triggered

  readonly property alias containsMouse: mouseArea.containsMouse

  // A Bar.Location value, shared with bars and popouts
  property int edge: Bar.Right
  // Along the edge: 0-1 of the free length, plus pixels
  property real position: 0.5
  property real positionOffset: 0

  // Depth across the edge, and length along it
  property int triggerWidth: PopoutConfig.edgeTriggerSize
  property int triggerLength: 200
  property int hoverDelay: PopoutConfig.openDelay

  // Integrated edge menus' zones (EdgeMenuManager) move the surfaces this
  // triggers inwards, so the trigger follows: past a zone on its own edge,
  // and along it as the edge's free length shrinks from either end
  readonly property bool _vertical: root.edge === Bar.Left || root.edge === Bar.Right
  property real edgeInset: EdgeMenuManager.zoneOn(root.screen?.name ?? "", Bar.edgeName(root.edge))
  property real startInset: EdgeMenuManager.zoneOn(root.screen?.name ?? "", root._vertical ? "top" : "left")
  property real endInset: EdgeMenuManager.zoneOn(root.screen?.name ?? "", root._vertical ? "bottom" : "right")
  // The trigger's centre along the edge, within the free length
  readonly property real _centre: root.startInset + ((root._vertical ? root.screen.height : root.screen.width) - root.startInset - root.endInset) * root.position + root.positionOffset

  color: "transparent"
  exclusionMode: ExclusionMode.Ignore

  // Anchor the attach edge, plus both sides of the perpendicular axis: a
  // wlr-layer-shell margin only takes effect on an anchored edge, and
  // positioning along the edge (the margins below) needs both anchored,
  // not just one, else the surface centers itself.
  anchors {
    left: root.edge !== Bar.Right
    right: root.edge !== Bar.Left
    top: root.edge !== Bar.Bottom
    bottom: root.edge !== Bar.Top
  }

  implicitWidth: root._vertical ? root.triggerWidth : root.triggerLength
  implicitHeight: root._vertical ? root.triggerLength : root.triggerWidth

  margins {
    left: root.edge === Bar.Left ? root.edgeInset : root._vertical ? 0 : Math.max(0, root._centre - root.triggerLength / 2)
    right: root.edge === Bar.Right ? root.edgeInset : root._vertical ? 0 : Math.max(0, root.screen.width - root._centre - root.triggerLength / 2)
    top: root.edge === Bar.Top ? root.edgeInset : !root._vertical ? 0 : Math.max(0, root._centre - root.triggerLength / 2)
    bottom: root.edge === Bar.Bottom ? root.edgeInset : !root._vertical ? 0 : Math.max(0, root.screen.height - root._centre - root.triggerLength / 2)
  }

  MouseArea {
    id: mouseArea
    anchors.fill: parent
    hoverEnabled: true
    onEntered: hoverTimer.restart()
    onExited: hoverTimer.stop()
  }

  Timer {
    id: hoverTimer
    interval: root.hoverDelay
    onTriggered: {
      if (mouseArea.containsMouse)
        root.triggered();
    }
  }
}
