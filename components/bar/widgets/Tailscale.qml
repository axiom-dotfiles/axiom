pragma ComponentBehavior: Bound

import QtQuick

import qs.services
import qs.config

BarIconWidget {
  id: root

  readonly property bool isConnected: TailscaleManager.connected
  readonly property string tailnetName: TailscaleManager.tailnetName

  readonly property var tailscaleRequest: ({
      "interval": root.properties.interval
    })
  onTailscaleRequestChanged: TailscaleManager.acquire(root, tailscaleRequest)
  Component.onCompleted: TailscaleManager.acquire(root, tailscaleRequest)
  Component.onDestruction: TailscaleManager.release(root)

  icon: isConnected ? "shield_lock" : "signal_disconnected"
  text: isConnected ? (properties.label || tailnetName) : ""
  showText: properties.showLabel

  backgroundColor: Theme.resolveColor(isConnected ? properties.connectedColor : properties.disconnectedColor)

  iconScale: 1.1
  textScale: 0.9

  clickable: true
  onClicked: NotificationManager.sendNotification("axiom", "Tailscale", root.isConnected ? I18n.tr("Connected to: {0}", root.tailnetName) : I18n.tr("Disconnected"))
}
