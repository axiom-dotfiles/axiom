pragma ComponentBehavior: Bound
import QtQuick
import Quickshell
import Quickshell.Io

import qs.components.hosts.popout
import qs.components.reusable
import qs.config
import qs.services

// The search launcher on one screen. LauncherManager builds the rows from
// the text and runs them, LauncherPanel shows them, and this picks the
// host, placed as a dock or OSD is (an edge, a position along it,
// detached or not): detached, a FloatingPopout `distance` across the free
// screen from its edge; else an EdgePopout that grows out of the border or
// bar there. Both have the same API (show/hide/isOpen/contentItem/window),
// and only the one in use is created.
Scope {
  id: root

  required property ShellScreen screen

  readonly property var host: LauncherConfig.detached ? floatingLoader.item : edgeLoader.item
  readonly property bool shown: root.host?.isOpen ?? false
  // What the panel searches for when it's created (it only exists while
  // the host is open)
  property string _openText: ""

  function open(text) {
    root._openText = text ?? "";
    // Searched first, so instances this opens on other monitors (see
    // SurfaceGroup) show the same text
    LauncherManager.query(root._openText);
    if (!root.host)
      return;
    if (root.host.isOpen)
      root.host.contentItem?.reset(root._openText);
    else
      root.host.show();
  }

  function close() {
    root.host?.hide();
  }

  function toggle() {
    if (root.shown)
      close();
    else
      open("");
  }

  // On every monitor, the instances open and close together
  SurfaceGroup {
    id: group
    kind: "launcher"
    mode: LauncherConfig.monitors
    screen: root.screen
    window: root.host?.window ?? null
    shown: root.shown
    onSyncRequested: shown => shown ? root.open(LauncherManager.text) : root.close()
  }

  Connections {
    target: ShellManager
    enabled: ShellManager.isTarget(root.screen, LauncherConfig.monitors)
    function onToggleAppLauncher() {
      root.toggle();
    }
  }

  // Locking from anywhere closes it, so it isn't still up on unlock
  Connections {
    target: LockManager
    function onLockStarted() {
      root.close();
    }
  }

  IpcHandler {
    target: "appLauncher"
    enabled: ShellManager.isTarget(root.screen, LauncherConfig.monitors)

    function toggle(): void {
      root.toggle();
    }
    // Not "show": `qs ipc call <target> show` is taken by the CLI
    function open(): void {
      if (!root.shown)
        root.open("");
    }
    function close(): void {
      root.close();
    }
    // Opens with `text` searched, e.g. "/theme " or "="
    function search(text: string): void {
      root.open(text);
    }
  }

  // The dim background, under the bars and border, for either host
  ScreenBackdrop {
    screen: root.screen
    shown: root.shown && LauncherConfig.showBackdrop
    fillColor: Theme.background
    fillOpacity: LauncherConfig.backdrop
  }

  // Detached, its distance 0 is where a detached dock or OSD on its edge
  // sits with an automatic gap (BarManager.detachedGaps): the free area's margin
  // on each side, in from the window, which starts past what's reserved
  // there
  readonly property var _edgeGaps: BarManager.detachedGaps(root.screen, LauncherConfig.edge, -1)
  readonly property var _oppositeGaps: BarManager.detachedGaps(root.screen, Bar.oppositeOf(LauncherConfig.edge), -1)
  function _marginOn(side) {
    const vertical = LauncherConfig.edge === Bar.Left || LauncherConfig.edge === Bar.Right;
    let gap = root._edgeGaps.end;
    if (side === LauncherConfig.edge)
      gap = root._edgeGaps.across;
    else if (side === Bar.oppositeOf(LauncherConfig.edge))
      gap = root._oppositeGaps.across;
    else if (side === (vertical ? Bar.Top : Bar.Left))
      gap = root._edgeGaps.start;
    return Math.max(0, EdgeMenuManager.frameLineOn(root.screen, side) + gap - EdgeMenuManager.reservedOn(root.screen, side));
  }

  LazyLoader {
    id: floatingLoader
    active: LauncherConfig.detached

    FloatingPopout {
      id: floating

      readonly property bool vertical: LauncherConfig.edge === Bar.Left || LauncherConfig.edge === Bar.Right
      // How far across from its edge: the fraction with the same align
      // spreads the room left over, 0 against the edge, 0.5 centred
      readonly property real across: LauncherConfig.edge === Bar.Bottom || LauncherConfig.edge === Bar.Right ? 1 - LauncherConfig.distance : LauncherConfig.distance

      screen: root.screen
      // Its centre at `position` along the edge
      xFraction: floating.vertical ? floating.across : LauncherConfig.position
      xAlign: floating.vertical ? floating.across : 0.5
      yFraction: floating.vertical ? LauncherConfig.position : floating.across
      yAlign: floating.vertical ? 0.5 : floating.across
      leftMargin: root._marginOn(Bar.Left)
      topMargin: root._marginOn(Bar.Top)
      rightMargin: root._marginOn(Bar.Right)
      bottomMargin: root._marginOn(Bar.Bottom)
      // Resized as results come and go, the box would move
      maxContentHeight: (floating.contentItem as LauncherPanel)?.maxHeight ?? 0
      // The panel's list glides to its rows' height itself
      animateHeight: false
      // Reversed, the search field stays put at the bottom
      growUp: LauncherConfig.reverse
      contentPadding: Appearance.borderWidth
      strokeColor: Theme.border
      closedScale: 0.97
      layerNamespace: "axiom-launcher"
      wantsKeyboardFocus: true
      closeOnClickOutside: true
      grabEnabled: group.ownsGrab
      grabWindows: group.windows.filter(w => w !== floating.window)

      content: Component {
        LauncherPanel {
          // Stays open until Esc, a pick or a click elsewhere
          readonly property bool autoDismiss: false
          implicitWidth: Math.min(LauncherConfig.width, root.screen.width - 32)
          shown: floating.isOpen
          onCloseRequested: floating.hide()
          Component.onCompleted: reset(root._openText)
        }
      }
    }
  }

  LazyLoader {
    id: edgeLoader
    active: !LauncherConfig.detached

    EdgePopout {
      id: popout
      screen: root.screen
      edge: LauncherConfig.edge
      position: LauncherConfig.position
      triggerEnabled: false
      wantsKeyboardFocus: true
      closeOnClickOutside: true
      grabEnabled: group.ownsGrab
      grabWindows: group.windows.filter(w => w !== popout.window)
      // Resized as results come and go, the window would jump (on a side
      // edge the panel holds its height instead, which runs along it)
      maxContentDepth: popout.vertical ? 0 : (popout.contentItem as LauncherPanel)?.maxHeight ?? 0

      content: Component {
        LauncherPanel {
          // Stays open until Esc, a pick or a click elsewhere
          readonly property bool autoDismiss: false
          implicitWidth: Math.min(LauncherConfig.width, root.screen.width - 64)
          holdHeight: popout.vertical
          shown: popout.isOpen
          onCloseRequested: popout.hide()
          Component.onCompleted: reset(root._openText)
        }
      }
    }
  }
}
