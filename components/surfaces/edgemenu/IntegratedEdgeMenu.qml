pragma ComponentBehavior: Bound
import QtQuick
import Quickshell
import Quickshell.Wayland

import qs.config
import qs.services
import qs.components.methods
import qs.components.hosts.popout

// An integrated edge menu: a Top-layer strip along the whole edge whose
// exclusive zone is arranged before everything else's (the axiom-edge-menu
// layer rule, HyprlandManager._addLayerRules), so it sits outermost and the
// border, bars and windows move inwards while it's open. The zone is set
// once when it opens (windows retile in one step), then the strip slides
// in; closing slides it out before the zone goes. With `frame` on, a
// rounded box runs the strip's whole length (its stroke ending the screen
// margin in from its edges, lining up with the screen border's) and the
// modules sit in it, `padding` in from its stroke. Along the edge they sit
// on the menu's lattice (GridPlacement.menuAlong).
//
// Open/close and hover-loss dismissal are PopoutWrapperBase's, as for the
// popouts.
PopoutWrapperBase {
  id: root

  required property var menu
  required property ShellScreen screen

  readonly property int edge: EdgeMenusConfig.edgeOf(root.menu)
  readonly property bool vertical: root.edge === Bar.Left || root.edge === Bar.Right

  readonly property bool framed: root.menu.frame
  readonly property int padding: EdgeMenusConfig.paddingOf(root.menu)
  readonly property var colors: EdgeMenusConfig.colorsOf(root.menu)
  // The frame's inset from the strip's edges (0 without one): the screen
  // margin reaches its stroke's inner edge, as the screen border's does, so
  // the two strokes line up
  readonly property int frameInset: root.framed ? Math.max(0, Appearance.screenMargin - Appearance.borderWidth) : 0
  // From the strip's edges to the cards: the frame and its stroke, then
  // the padding
  readonly property int pad: root.frameInset + (root.framed ? Appearance.borderWidth : 0) + root.padding
  // With the screen border off, the strip's own inner stroke (with it on,
  // the border's strip inside this one draws it)
  readonly property int innerStroke: Appearance.screenBorder ? 0 : Appearance.borderWidth
  readonly property real edgeLength: root.vertical ? root.screen.height : root.screen.width
  readonly property real bodyDepth: root.vertical ? (loader.item?.implicitWidth ?? 0) : (loader.item?.implicitHeight ?? 0)
  readonly property int depth: Math.ceil(root.bodyDepth + root.pad * 2 + root.innerStroke)
  // Where the modules start along the edge, from config (the body loads
  // only while open, and the hover strip needs it closed)
  // Its card size (EdgeMenuManager.cardUnitOf)
  readonly property int unit: EdgeMenuManager.cardUnitOf(root.menu)
  readonly property real gridLength: EdgeMenusConfig.gridLengthOf(root.menu, root.vertical, root.unit)
  readonly property real alongPos: GridPlacement.menuAlong(root.menu, root.edgeLength, root.unit, root.pad, root.pad)

  // Where the modules can sit, for the layouts editor (EdgeMenuManager.frames)
  readonly property var frame: ({
      "screen": root.screen?.name ?? "",
      "startPad": root.pad,
      "endPad": root.pad,
      "across": root.pad,
      "before": 0,
      "after": root.pad + root.innerStroke,
      "reserves": true
    })

  autoDismiss: sync.autoDismiss
  dismissDelay: root.menu.closeDelay
  keepAlive: panelHover.hovered || trigger.containsMouse

  EdgeMenuSync {
    id: sync
    host: root
    menu: root.menu
    screen: root.screen
    window: panel
    frame: root.frame
    onWarpRequested: HyprlandManager.warpCursorToLayer("axiom-edge-menu", root.screen?.name ?? "", panel.width, panel.height, loader.x + loader.width / 2, loader.y + loader.height / 2)
  }
  Component.onDestruction: EdgeMenuManager.setZone(root.screen?.name ?? "", root._edgeName, 0)

  // Report the space taken, for surfaces laid out against this edge
  readonly property string _edgeName: Bar.edgeName(root.edge)
  readonly property int reserved: panel.visible ? root.depth : 0
  onReservedChanged: EdgeMenuManager.setZone(root.screen?.name ?? "", root._edgeName, root.reserved)

  EdgeTrigger {
    id: trigger
    screen: root.screen
    visible: root.menu.openOnHover
    edge: root.edge
    // On the modules (the strip spans the whole edge)
    centre: root.menu.length === "edge" ? root.edgeLength / 2 : root.alongPos + root.gridLength / 2
    // Not its own zone, and the strip spans the whole edge regardless of
    // the others
    edgeInset: Math.max(0, EdgeMenuManager.zoneOn(root.screen?.name ?? "", root._edgeName) - root.reserved)
    startInset: 0
    endInset: 0
    triggerWidth: root.menu.triggerSize
    triggerLength: sync.triggerLength(loader.item, root.vertical, root.pad)
    hoverDelay: root.menu.openDelay
    onTriggered: root.show()
  }

  PanelWindow {
    id: panel
    screen: root.screen
    // Mapped only while open: the zone (and the windows moving) goes with it
    visible: root.occupied && loader.status === Loader.Ready
    color: "transparent"

    WlrLayershell.layer: WlrLayer.Top
    WlrLayershell.namespace: "axiom-edge-menu"
    WlrLayershell.keyboardFocus: WlrKeyboardFocus.OnDemand
    exclusionMode: ExclusionMode.Normal
    exclusiveZone: root.depth

    anchors {
      top: root.edge === Bar.Top || root.vertical
      bottom: root.edge === Bar.Bottom || root.vertical
      left: root.edge === Bar.Left || !root.vertical
      right: root.edge === Bar.Right || !root.vertical
    }

    implicitWidth: root.vertical ? root.depth : 0
    implicitHeight: root.vertical ? 0 : root.depth

    SlideAnimation {
      anchors.fill: parent
      active: root.isOpen
      slideFromLeft: root.edge === Bar.Left
      slideFromRight: root.edge === Bar.Right
      slideFromTop: root.edge === Bar.Top
      slideFromBottom: root.edge === Bar.Bottom
      enableFade: false
      containerWidth: panel.width
      containerHeight: panel.height

      // One item, since the slide's content takes items only
      Item {
        anchors.fill: parent

        HoverHandler {
          id: panelHover
        }

        // The strip, in the border's colours: the screen frame grown by the
        // menu (without a frame, the menu's own background)
        Rectangle {
          anchors.fill: parent
          color: root.framed ? Theme.background : root.colors.fill
        }

        // The frame: a rounded box along the whole strip
        Rectangle {
          visible: root.framed
          // Clear of the strip's inner stroke, which is at its start on a
          // right or bottom strip
          x: root.frameInset + (root.edge === Bar.Right ? root.innerStroke : 0)
          y: root.frameInset + (root.edge === Bar.Bottom ? root.innerStroke : 0)
          width: parent.width - root.frameInset * 2 - (root.vertical ? root.innerStroke : 0)
          height: parent.height - root.frameInset * 2 - (root.vertical ? 0 : root.innerStroke)
          radius: Appearance.borderRadius
          color: root.colors.fill
          border.color: root.colors.stroke
          border.width: Appearance.borderWidth
        }

        // With the screen border off, nothing inside draws the frame's stroke
        Rectangle {
          visible: !Appearance.screenBorder
          color: Theme.foreground
          width: root.vertical ? Appearance.borderWidth : parent.width
          height: root.vertical ? parent.height : Appearance.borderWidth
          x: root.edge === Bar.Left ? parent.width - width : 0
          y: root.edge === Bar.Top ? parent.height - height : 0
        }

        Loader {
          id: loader
          active: root.occupied

          readonly property real across: root.pad + (root.edge === Bar.Right || root.edge === Bar.Bottom ? root.innerStroke : 0)

          x: root.vertical ? across : root.alongPos
          y: root.vertical ? root.alongPos : across

          sourceComponent: EdgeMenuBody {
            menu: root.menu
            vertical: root.vertical
            maxLength: root.edgeLength - root.pad * 2
          }

          onLoaded: root.updateDismissTimer()
        }
      }
    }
  }
}
